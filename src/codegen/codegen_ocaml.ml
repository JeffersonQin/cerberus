(* Created by Victor Gomes 2017-03-10 *)

open Util
open Core
open Cps_core
open Core_opt
open Pp_prelude
open Pp_ocaml

open Rt_ocaml

let tt = ivmax

let print_head filename =
  !^"(* Generated by Cerberus from" ^^^ !^filename ^^ !^" *)" ^//^
  !^"module A = Rt_ocaml" ^/^
  !^"module M = Mem" ^/^
  !^"module I = Mem.Impl" ^/^
  !^"module T = AilTypes" ^/^
  !^"module C = Core_ctype" ^/^
  !^"module O = Util.Option" ^//^
  !^"let (>>=) = M.bind2" ^//^
  (*
  !^"let (>>=) = Continuation.bind" ^//^
     *)
  !^"let _std_function_printf cont x y = (A.value [(I.IV (I.Prov_none, \
                            I.IVconcrete (Nat_big_num.of_string \"0\")))])" ^/^
  !^"let kill x = A.value ()"

let print_premain globs =
  let globals acc sym =
    let pp_sym = print_symbol sym in
    acc ^^ !^"glob_" ^^ pp_sym ^^^ !^"M.return2 () >>= fun _" ^^ pp_sym ^^^ !^"->"
      ^^^ pp_sym ^^^ !^":= _" ^^ pp_sym ^^ P.semi ^^ P.break 1
  in
  !^"let premain cont () =" ^^ !> (
    List.fold_left globals P.empty globs ^^ !^"main cont ();;"
  )

let print_foot =
  !^"A.quit premain"

let opt_passes core =
  elim_wseq core
  |> assoc_seq
  |> elim_skip

(* Generate Ocaml *)
let generate_ocaml sym_supply core =
  let cps_core = cps_transform sym_supply (run opt_passes core) in
  let globs_syms = List.map (fun (s,_,_,_) -> s) cps_core.globs in
  let globals acc (sym, coreTy, bbs, bbody) =
    acc
    ^^ !^"and" ^^^ print_eff_function (!^"glob_" ^^ print_symbol sym
                                       ^^^ print_symbol default) []
      (print_base_type coreTy) (print_transformed globs_syms bbs bbody)
    ^/^ !^"and" ^^^ print_symbol sym ^^^ !^"= ref (M.null_ptrval Core_ctype.Void0)"
  in
    print_impls globs_syms cps_core.impl ^^
    print_funs globs_syms cps_core.stdlib ^//^
    List.fold_left globals P.empty cps_core.globs ^^
    print_funs globs_syms cps_core.funs ^//^
    print_premain globs_syms

let compile filename sym_supply core =
  let fl = Filename.chop_extension filename in
  let fl_ml = fl ^ ".ml" in
  let oc = open_out fl_ml in
  begin
    P.ToChannel.pretty 1. 80 oc
      (print_head filename ^^ generate_ocaml sym_supply core ^//^ print_foot);
    close_out oc;
    Exception.return0 0
  end
